<?xml version="1.0" encoding="utf-8"?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
"http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" lang="en" xml:lang="en">
<head>
<!-- 2017-07-17 Mon 13:06 -->
<meta http-equiv="Content-Type" content="text/html;charset=utf-8" />
<meta name="viewport" content="width=device-width, initial-scale=1" />
<title>Coordinates &amp; Transforms in 2D</title>
<meta name="generator" content="Org mode" />
<meta name="author" content="Chris Hodapp" />
<link rel="stylesheet" href="css/tutorial.css"/>
<link rel="stylesheet" href="css/code.css"/>
<script type="text/javascript" charset="utf-8">if(!window.PolymerGestures) { var head = document.getElementsByTagName('head')[0]; var js = document.createElement("script"); js.type = "text/javascript"; js.src = "//cdn.jsdelivr.net/webcomponentsjs/0.7.20/webcomponents.min.js"; head.appendChild(js); } </script>
<link rel='import' href='juicy-ace-editor.html'/>
<script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.1/MathJax.js?config=TeX-AMS-MML_HTMLorMML"></script>
</head>
<body>
<div id="content">
<h1 class="title">Coordinates &amp; Transforms in 2D</h1>

<div id="outline-container-orgb8c3161" class="outline-2">
<h2 id="orgb8c3161"><span class="section-number-2"> 2</span> Coordinates &amp; Transforms in 2D</h2>
<div class="outline-text-2" id="text-1">
</div><div id="outline-container-org39fcf67" class="outline-3">
<h3 id="org39fcf67"><span class="section-number-3"> 2.1</span> A slightly boring, laborious example</h3>
<div class="outline-text-3" id="text-1-1">
<p>
The Introduction mentioned that all the code this tutorial deals with
is <i>fragment shaders</i> (or pixel shaders) in GLSL.  For our sake, what
that means is that we are dealing with short programs which create
images (and eventually animations) by explicitly giving the color of
every pixel in the image.
</p>

<p>
This may be a bit different than what you are used to in more general
programming languages.  You are likely used to programs for which, at
any given point in time, variables have a specific value and the
program is executing some specific part of the code.
</p>

<p>
Fragment shaders deviate from this a bit, and require a bit of a
different mindset.  The way we use them here, they can be thought of
as programs which run simultaneously over every pixel in the image,
and likewise, have variables which take on different values for every
pixel.  (See <a target='_blank' href="https://en.wikipedia.org/wiki/Stream_processing">Stream processing</a> for a little more in-depth information
on this.)
</p>

<p>
The below code gives a very simple example of this:
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-1' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;

void main()
{
  vec2 uv = gl_FragCoord.xy / resolution.xy;
  gl_FragColor = vec4(uv.x, uv.y, 0.0, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-1' style='display:none'>uniform vec2 resolution;

void main()
{
  vec2 uv = gl_FragCoord.xy / resolution.xy;
  gl_FragColor = vec4(uv.x, uv.y, 0.0, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-1').text());">Use code &raquo;</button></div>
</div>

<p>
Click <b>Use code</b>, and then go to the <b>Render</b> tab.  You should see a
gradient - green at the top left corner, yellow at the top right, red
at the bottom right, black at the bottom left, and colors blended
everywhere between those.
</p>

<p>
Click back to the <b>Fragment Shader</b> tab (ignore the <b>Parameters</b> tab
for now).  The code might look familiar if you've used something like
C, C++, or Java, but with a few unfamiliar constructs.  <code>vec2</code> and
<code>vec4</code> are vector types containing 2 elements and 4 elements,
respectively (more on that later).  For now, just treat a <code>vec2</code> as a
structure with fields <code>x</code> and <code>y</code>, and a <code>vec4</code> as a structure with
fields <code>r</code>, <code>g</code>, <code>b</code>, and <code>a</code>, all of them floating-point values.
</p>

<p>
<code>gl_FragCoord</code> is one of those variables which takes on a different
value for every pixel in the image; the <code>.xy</code> provides us a <code>vec2</code> of
just the X and Y coordinates of each pixel.  <code>resolution</code>, on the
other hand, is a <i>uniform</i> variable - it has the same value over all
pixels.  In this case, it's something the environment provides to tell
us the total number of pixels along X and Y.
</p>

<p>
<code>gl_FragCoord.xy / resolution.xy</code> produces a new <code>vec2</code> by dividing
<code>gl_FragCoord.x</code> by <code>resolution.x</code> and <code>gl_FragCoord.y</code> by
<code>resolution.y</code>.  Try the code below if you want to verify yourself
that it's the same as splitting them out:
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-2' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;

void main()
{
  float u = gl_FragCoord.x / resolution.x;
  float v = gl_FragCoord.y / resolution.y;
  gl_FragColor = vec4(u, v, 0.0, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-2' style='display:none'>uniform vec2 resolution;

void main()
{
  float u = gl_FragCoord.x / resolution.x;
  float v = gl_FragCoord.y / resolution.y;
  gl_FragColor = vec4(u, v, 0.0, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-2').text());">Use code &raquo;</button></div>
</div>

<p>
Thus, the <code>uv</code> vector (or <code>u</code> and <code>v</code> separately above) contains X and
Y coordinates that each have been <i>normalized</i> range from 0 to 1.
Since it's dividing by the total resolution, it works identically at
any resolution (resize the render window and see, or click the
lower-right icon to make it full-screen).  This gives us a normal
<i>(x,y)</i> coordinate plane (or Cartesian plane) where the lower-left
point is (0,0), moving to the right increases the X coordinate, and
moving up increases the Y coordinate - up to the top-right corner
which is (1,1).  This is a fairly standard transformation to see
anytime pixel locations are involved.
</p>

<p>
Finally, <code>gl_FragColor</code> is where we assign the pixel's respective
color into a <code>vec4</code> by giving, in order, its red, green, blue, and
alpha channel values (and we ignore alpha here, as it refers to
transparency and doesn't make sense to use here).  Each value is from
0 to 1, and values outside that range are clipped inside of it; try
messing with the values (e.g. replacing <code>u</code> with <code>u * 4.0</code>) to verify
this.
</p>

<p>
The result, then, is that we've assigned the red channel of each pixel
to its X coordinate, and the green channel to the Y coordinate.  The
blue channel was just left at 0.  Thus, at the very lower-left, we'd
expect to see black (red and green are both 0 because X and Y are
zero), and moving up to the top-right, we'd expect to see yellow (red
and green are both at their maximum of 1).
</p>
</div>
</div>

<div id="outline-container-org7ba5ecd" class="outline-3">
<h3 id="org7ba5ecd"><span class="section-number-3"> 2.2</span> What can this actually do?</h3>
<div class="outline-text-3" id="text-1-2">
<p>
You may be complaining that a colored gradient is a rather
uninteresting thing to draw, and it is.  However, with some additional
math, this method of creating graphics is remarkably flexible.
</p>

<p>
First off, though, we must be able to create some basic shapes and
constructs to demonstrate things.  We've already shown how the
computation of <code>uv</code> above can create simple color gradients - but
color gradients by their inherent fuzziness make it difficult to show
any sort of sharp details or well-defined shapes.
</p>

<p>
So, consider the below code.  The <code>uv.x</code> line isn't especially
important, but it will appear again and again, and it is there to
correct the <a target='_blank' href="https://en.wikipedia.org/wiki/Aspect_ratio_%28image%29">aspect ratio</a> of the image.  In our case, all it means is
that <code>uv.x</code> will be scaled so that a given step in <code>uv.x</code> is the same
distance as in <code>uv.y</code>, and our shapes aren't squashed.
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-3' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;
float count = 10.0;

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    vec2 uv_mod = mod(uv * count, 1.0);
    
    gl_FragColor = vec4(uv_mod.x, uv_mod.y, 0, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-3' style='display:none'>uniform vec2 resolution;
float count = 10.0;

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    vec2 uv_mod = mod(uv * count, 1.0);
    
    gl_FragColor = vec4(uv_mod.x, uv_mod.y, 0, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-3').text());">Use code &raquo;</button></div>
</div>

<p>
The important line is the definition of <code>uv_mod</code>: we've scaled up <code>uv</code>
by a certain amount in both X and Y, and then used <a target='_blank' href="https://en.wikipedia.org/wiki/Modulo_operation">modulo</a> to turn it
to a repeating pattern again in X and Y - across some distance, it
rises from 0 to 1, and then goes back to 0.
</p>

<p>
You should be able to see a sort of grid pattern emerging, and playing
with the value of <code>count</code>, or changing the <code>1.0</code> in <code>mod(..., 1.0)</code> to
something else, should produce some effects that make sense.  However,
if you look, you'll see it's still just a bunch of smaller gradients.
Try looking just at <code>uv_mod.x</code> or just <code>uv_mod.y</code> (i.e. change
<code>gl_FragColor</code> so that the red, green, and blue channel are all
<code>uv_mod.x</code>, and then so they all are <code>uv_mod.y</code>).
</p>

<p>
Now consider: How could we turn this into a grid with sharp lines,
instead of gradients?  Try to make sense of the below, and change
values like <code>thickness</code> to something else:
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-4' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;
float count = 10.0;
float thickness = 0.05;

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    vec2 uv_mod = mod(uv * count, 1.0);
    float x1 = uv_mod.x &lt; thickness ? 1.0 : 0.0;
    float y1 = uv_mod.y &lt; thickness ? 1.0 : 0.0;
    
    gl_FragColor = vec4(x1, y1, 0, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-4' style='display:none'>uniform vec2 resolution;
float count = 10.0;
float thickness = 0.05;

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    vec2 uv_mod = mod(uv * count, 1.0);
    float x1 = uv_mod.x &lt; thickness ? 1.0 : 0.0;
    float y1 = uv_mod.y &lt; thickness ? 1.0 : 0.0;
    
    gl_FragColor = vec4(x1, y1, 0, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-4').text());">Use code &raquo;</button></div>
</div>

<p>
This is a fairly small change from the previous code.  If you're not
familiar, <code>uv_mod</code> is now defined using the <a target='_blank' href="https://en.wikipedia.org/wiki/%3F:#C">ternary operator</a> that is
commonly used in C.  In simple, it has forced the darker parts (see
<code>thickness</code>) to be uniformly light, and the parts other than that to
be uniformly black.  However, the X and Y grid lines are still
separated out, and we may simply add them together to give white grid
lines:
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-5' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;
float count = 10.0;
float thickness = 0.05;

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    vec2 uv_mod = mod(uv * count, 1.0);
    float x1 = uv_mod.x &lt; thickness ? 1.0 : 0.0;
    float y1 = uv_mod.y &lt; thickness ? 1.0 : 0.0;
    float grey = x1 + y1;
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-5' style='display:none'>uniform vec2 resolution;
float count = 10.0;
float thickness = 0.05;

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    vec2 uv_mod = mod(uv * count, 1.0);
    float x1 = uv_mod.x &lt; thickness ? 1.0 : 0.0;
    float y1 = uv_mod.y &lt; thickness ? 1.0 : 0.0;
    float grey = x1 + y1;
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-5').text());">Use code &raquo;</button></div>
</div>
</div>
</div>

<div id="outline-container-orgbb148c3" class="outline-3">
<h3 id="orgbb148c3"><span class="section-number-3"> 2.3</span> Implicit functions</h3>
<div class="outline-text-3" id="text-1-3">
<p>
Perhaps it wasn't obvious why this is significant, but to try to
explain it further: We just used an <a target='_blank' href="https://en.wikipedia.org/wiki/Implicit_function">implicit function</a> to draw lines.
Rather than drawing lines by iteratively walking along pixel
coordinates and darkening certain ones according to a line's formula,
we started with a formula that was something like:
</p>

\begin{equation}
   f(x,y)=I(x\mod C)+I(y\mod C) \mathrm{ where}\\
   I(a) = \begin{cases}
   a < \epsilon & : 1\\
   a \ge \epsilon & : 0
   \end{cases}
\end{equation}

<p>
and then evaluated this over every pixel, using \(C\) as basically
<code>count</code> and \(\epsilon\) as <code>thickness</code>.  We've taken some liberties, in
that we're comparing with \(\epsilon\) rather than 0, but that is due to
using floating point and a discrete number of pixels. If we break this
(mathematical) function out into a separate (GLSL) function:
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-6' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = uv_mod.x &lt; thickness ? 1.0 : 0.0;
    float y1 = uv_mod.y &lt; thickness ? 1.0 : 0.0;
    return x1 + y1;
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    float grey = grid(uv, 0.05, 10.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-6' style='display:none'>uniform vec2 resolution;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = uv_mod.x &lt; thickness ? 1.0 : 0.0;
    float y1 = uv_mod.y &lt; thickness ? 1.0 : 0.0;
    return x1 + y1;
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    float grey = grid(uv, 0.05, 10.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-6').text());">Use code &raquo;</button></div>
</div>

<p>
Put another way, we just drew <a target='_blank' href="https://en.wikipedia.org/wiki/Contour_line">isolines</a> of that function.
</p>

<p>
More about this implicit functions will follow later.  For now, we
just use it to create a grid to help illustrate some transformations.
Below is one that may be familiar: It converts the coordinates we're
using already - rectangular, or Cartesian, coordinates in <code>uv</code> - to
<a target='_blank' href="https://en.wikipedia.org/wiki/Polar_coordinate_system">polar coordinates</a>.  <code>uv</code> is also rescaled and moved so that the center
is (0,0).  Then, we use polar rather than rectangular coordinates to
draw this grid.
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-7' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;
const float PI = 3.14159265359;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = uv_mod.x &lt; thickness ? 1.0 : 0.0;
    float y1 = uv_mod.y &lt; thickness ? 1.0 : 0.0;
    return x1 + y1;
}

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(length(xy), atan(xy.y, xy.x));
    return polar;
}

void main()
{
    vec2 uv = 2.0 * gl_FragCoord.xy/resolution.xy - 1.0;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    vec2 polar = rect2polar(uv);
    polar.y = polar.y / PI;
    float grey = grid(polar, 0.05, 10.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-7' style='display:none'>uniform vec2 resolution;
const float PI = 3.14159265359;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = uv_mod.x &lt; thickness ? 1.0 : 0.0;
    float y1 = uv_mod.y &lt; thickness ? 1.0 : 0.0;
    return x1 + y1;
}

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(length(xy), atan(xy.y, xy.x));
    return polar;
}

void main()
{
    vec2 uv = 2.0 * gl_FragCoord.xy/resolution.xy - 1.0;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    vec2 polar = rect2polar(uv);
    polar.y = polar.y / PI;
    float grey = grid(polar, 0.05, 10.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-7').text());">Use code &raquo;</button></div>
</div>

<p>
Note that, despite the notation, fields <code>x</code> and <code>y</code> of <code>polar</code> now
stand for radius and angle, not X and Y coordinates.
</p>

<p>
We started with a grid made of equally-sized squares.  The above
should give some intuitive sense of how the conversion to polar
coordinates transformed space: The grid "squares" are now
differently-sized sections of a circle. 
</p>
</div>
</div>

<div id="outline-container-org54f2302" class="outline-3">
<h3 id="org54f2302"><span class="section-number-3"> 2.4</span> Animation &amp; Mouse Input</h3>
<div class="outline-text-3" id="text-1-4">
<p>
Up to this point, we've just been rendering images that don't change
over time or in response to any input (aside from you editing the
code).  The below code makes a couple modifications to the last
example to change this:
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-8' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;
const float PI = 3.14159265359;
uniform vec4 mouse;
uniform float time;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = clamp(thickness - uv_mod.x, 0.0, 1.0);
    float y1 = clamp(thickness - uv_mod.y, 0.0, 1.0);
    return (min(x1, thickness-x1) + min(y1,thickness-y1))/thickness;
}

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(sqrt(xy.x * xy.x + xy.y * xy.y), atan(xy.y, xy.x));
    return polar;
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    // Normalize mouse position:
    vec2 m = vec2(mouse.x/resolution.x, 1.0 - mouse.y/resolution.y);
    m.x = m.x * resolution.x / resolution.y;

    vec2 polar = rect2polar(uv - m);
    // Radius:
    polar.x = polar.x - time / 10.0;
    // Angle:
    polar.y = (polar.y / PI) + time / 8.0;
    float grey = grid(polar, 0.1, 10.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-8' style='display:none'>uniform vec2 resolution;
const float PI = 3.14159265359;
uniform vec4 mouse;
uniform float time;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = clamp(thickness - uv_mod.x, 0.0, 1.0);
    float y1 = clamp(thickness - uv_mod.y, 0.0, 1.0);
    return (min(x1, thickness-x1) + min(y1,thickness-y1))/thickness;
}

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(sqrt(xy.x * xy.x + xy.y * xy.y), atan(xy.y, xy.x));
    return polar;
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    // Normalize mouse position:
    vec2 m = vec2(mouse.x/resolution.x, 1.0 - mouse.y/resolution.y);
    m.x = m.x * resolution.x / resolution.y;

    vec2 polar = rect2polar(uv - m);
    // Radius:
    polar.x = polar.x - time / 10.0;
    // Angle:
    polar.y = (polar.y / PI) + time / 8.0;
    float grey = grid(polar, 0.1, 10.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-8').text());">Use code &raquo;</button></div>
</div>

<p>
(The <code>grid</code> function was rewritten to have lines with softer edges as
a primitive <a target='_blank' href="https://en.wikipedia.org/wiki/Spatial_anti-aliasing">anti-aliasing</a> approach.  You can try to understand how it
works, but it doesn't much matter.  If by chance it runs too slowly on
your GPU, feel free to copy &amp; paste the old <code>grid</code> definition in
instead.)
</p>

<p>
To see the render react to input, click or drag in the render window.
The environment provides two new variables - <code>mouse</code> and <code>time</code> -
which give, respectively, the mouse location and a time value (which
simply counts up in seconds).  (<code>mouse</code>, for whatever reason, uses a
different coordinate system than the rest of the image, so above we
remedy this when we compute <code>m</code> and then we do the same transformation
to normalize this mouse position as we do each pixel coordinate.)
</p>

<p>
There are two buttons at the bottom center of the render window which
let you stop animation completely, and pause or resume it.  It will
continue to react to mouse input even when paused or stopped.
</p>

<p>
In the last example, the center of the image was (0,0) (in both
rectangular and polar coordinates, incidentally); in this one, note
that by subtracting the mouse position, wherever you click is the new
origin.
</p>

<p>
See the changes to <code>polar.x</code> and <code>polar.y</code> for the source of the
animation; change how <code>time</code> is used (adding vs. subtracting, and
dividing by larger and smaller numbers), and it should make sense how
this works.
</p>
</div>
</div>

<div id="outline-container-org48c5bfb" class="outline-3">
<h3 id="org48c5bfb"><span class="section-number-3"> 2.5</span> Control Inputs</h3>
<div class="outline-text-3" id="text-1-5">
<p>
I mentioned <i>uniform</i> variables earlier, and we've dealt with a couple
of them like <code>resolution</code>, <code>mouse</code>, and <code>time</code>.  We've also introduced
some code which uses parameters here and there that (so far) had to be
edited manually in the code.
</p>

<p>
Helpfully, the environment we're working in also provides the ability
to create our own uniform variables and adjust them in the renderer.
Click <b>Use code</b> below and then look at the <b>Parameters</b> tab.
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-9' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;
const float PI = 3.14159265359;
uniform vec4 mouse;
uniform float time;

uniform float freq_rad;
uniform float freq_ang;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = clamp(thickness - uv_mod.x, 0.0, 1.0);
    float y1 = clamp(thickness - uv_mod.y, 0.0, 1.0);
    return (min(x1, thickness-x1) + min(y1,thickness-y1))/thickness;
}

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(sqrt(xy.x * xy.x + xy.y * xy.y), atan(xy.y, xy.x));
    return polar;
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    // Normalize mouse position:
    vec2 m = vec2(mouse.x/resolution.x, 1.0 - mouse.y/resolution.y);
    m.x = m.x * resolution.x / resolution.y;

    vec2 polar = rect2polar(uv - m);
    // Radius:
    polar.x = polar.x + time * freq_rad;
    // Angle:
    polar.y = (polar.y / PI) + time * freq_ang;
    float grey = grid(polar, 0.1, 10.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-9' style='display:none'>/* PARAMETERS
[
   {
       "type": "float",
       "value": 0.1,
       "min": -1,
       "max": 1,
       "name": "freq_rad",
       "GUIName": "FreqRad"
   },
   {
       "type": "float",
       "value": 0.1,
       "min": -1,
       "max": 1,
       "name": "freq_ang",
       "GUIName": "FreqAng"
   }
]
END */
uniform vec2 resolution;
const float PI = 3.14159265359;
uniform vec4 mouse;
uniform float time;

uniform float freq_rad;
uniform float freq_ang;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = clamp(thickness - uv_mod.x, 0.0, 1.0);
    float y1 = clamp(thickness - uv_mod.y, 0.0, 1.0);
    return (min(x1, thickness-x1) + min(y1,thickness-y1))/thickness;
}

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(sqrt(xy.x * xy.x + xy.y * xy.y), atan(xy.y, xy.x));
    return polar;
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    // Normalize mouse position:
    vec2 m = vec2(mouse.x/resolution.x, 1.0 - mouse.y/resolution.y);
    m.x = m.x * resolution.x / resolution.y;

    vec2 polar = rect2polar(uv - m);
    // Radius:
    polar.x = polar.x + time * freq_rad;
    // Angle:
    polar.y = (polar.y / PI) + time * freq_ang;
    float grey = grid(polar, 0.1, 10.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-9').text());">Use code &raquo;</button></div>
</div>

<p>
Note that the code (the <b>Fragment Shader</b> code) is near-identical to
the last example, except that:
</p>

<ul class="org-ul">
<li>Two <code>uniform float</code> variables are now declared, and their names
(<code>freq_rad</code> and <code>freq_ang</code>).</li>
<li>The names of the above two variables are identical to the <code>name</code>
field in the JSON definitions in the <b>Parameters</b> tab.</li>
<li>The lines with <code>polar.x</code> and <code>polar.y</code> now use the uniform</li>
</ul>

<p>
Click <b>Render</b> and see that a controls panel should now be visible at
the top right (you may need to click <b>Open Controls</b>).  It should have
sliders with the names you supplied in <code>GUIName</code> in the JSON
definitions.  Adjust these sliders, and you should see parts of the
animation proceed at different speeds (and directions, if you change
between positive and negative).
</p>

<p>
For some information on this type of variable, see <a target='_blank' href="http://pocket.gl/documentation/#GUI_Variables_uniforms">pocket.gl
documentation</a>.
</p>
</div>
</div>

<div id="outline-container-orgb2d676f" class="outline-3">
<h3 id="orgb2d676f"><span class="section-number-3"> 2.6</span> Function composition</h3>
<div class="outline-text-3" id="text-1-6">
<p>
In the prior examples, we achieved some simple transformations on
graphics by starting with the \((r,\theta)\) of polar coordinates above,
we achieved rotation by turning \(\theta\) to \(\theta+f_1t\) (where \(t\)
is something like the variable <code>time</code>, and \(f_1\) like <code>freq_ang</code>), and
we achieved a sort of radial expansion or contraction by turning \(r\)
to \(r+f_2t\).  If you look a little deeper, we're using the same method
to move the graphics around by turning rectangular coordinates \((x,y)\)
to \((x-x_m, y-y_m)\), where \((x_m, y_m)\) are the mouse coordinates.
</p>

<p>
More generally, in both cases we were changing the <a target='_blank' href="https://en.wikipedia.org/wiki/Domain_of_a_function">domain</a> of some
<b>function</b> (in mostly the mathematical sense of the term) by <a target='_blank' href="https://en.wikipedia.org/wiki/Function_composition">composing</a>
another function with it. Fundamentally, this fragment shader is just
a function which turns domain \((X,Y)\) - the pixel location - to
codomain \((r,g,b)\) - the pixel's color.
</p>

<p>
From this standpoint, we started out with a function itself made by
composing together several other functions in order (if "compose"
feels too odd of a term here, just think of it like steps a pipeline):
</p>

<ol class="org-ol">
<li>Converting pixel location <code>gl_FragCoord</code> to normalized pixel location <code>uv</code>.</li>
<li>Converting <code>uv</code> to polar coordinates <code>polar</code>.</li>
<li>Using <code>polar</code> to create a grid with function <code>grid</code> (consisting of
just values 0 and 1).</li>
<li>Turning those 0 &amp; 1 grid values into \((r,g,b)\) in <code>gl_FragColor</code>.</li>
</ol>

<p>
As we added in the effect of the mouse and the animation, we spliced
in additional functions - first in between 1 and 2 for the mouse, then
in between 2 and 3 for the animation.
</p>

<p>
To give another example, let's start with the simple rectangular grid
we've seen before - but warp the domain of <code>grid</code> a little.  Look
particularly at the <code>sine_warp</code> function, and note that what in place
of <code>uv</code> (the input to <code>grid</code>) we've instead used <code>uv</code> as the input to
<code>sine_warp</code>, and put its output in place of <code>uv</code>.
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-10' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;
uniform float time;
uniform vec4 mouse;

uniform float freq_r;
uniform float freq_t;
uniform float ampl;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = clamp(thickness - uv_mod.x, 0.0, 1.0);
    float y1 = clamp(thickness - uv_mod.y, 0.0, 1.0);
    return (min(x1, thickness-x1) + min(y1,thickness-y1))/thickness;
}

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(sqrt(xy.x * xy.x + xy.y * xy.y), atan(xy.y, xy.x));
    return polar;
}

vec2 polar2rect(vec2 polar)
{
    vec2 rect = vec2(polar.x * cos(polar.y), polar.x * sin(polar.y));
    return rect;
}

vec2 sine_warp(vec2 xy, float ampl, float freq, float phase)
{
    vec2 polar = rect2polar(xy);
    float rad = polar.x + ampl*sin(freq * polar.x + phase);
    return polar2rect(vec2(rad, polar.y));
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    // Normalize mouse position:
    vec2 m = vec2(mouse.x/resolution.x, 1.0 - mouse.y/resolution.y);
    m.x = m.x * resolution.x / resolution.y;

    vec2 uv2 = sine_warp(uv - m, ampl, freq_r, -time*freq_t);
    float grey = grid(uv2, 0.1, 20.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-10' style='display:none'>/* PARAMETERS
[
   {
       "type": "float",
       "value": 17,
       "min": 0,
       "max": 50,
       "name": "freq_r",
       "GUIName": "FreqR"
   },
   {
       "type": "float",
       "value": 3.5,
       "min": 0,
       "max": 10,
       "name": "freq_t",
       "GUIName": "FreqT"
   },
   {
       "type": "float",
       "value": 0.02,
       "min": 0,
       "max": 0.2,
       "name": "ampl",
       "GUIName": "Ampl"
   }
]
END */
uniform vec2 resolution;
uniform float time;
uniform vec4 mouse;

uniform float freq_r;
uniform float freq_t;
uniform float ampl;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = clamp(thickness - uv_mod.x, 0.0, 1.0);
    float y1 = clamp(thickness - uv_mod.y, 0.0, 1.0);
    return (min(x1, thickness-x1) + min(y1,thickness-y1))/thickness;
}

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(sqrt(xy.x * xy.x + xy.y * xy.y), atan(xy.y, xy.x));
    return polar;
}

vec2 polar2rect(vec2 polar)
{
    vec2 rect = vec2(polar.x * cos(polar.y), polar.x * sin(polar.y));
    return rect;
}

vec2 sine_warp(vec2 xy, float ampl, float freq, float phase)
{
    vec2 polar = rect2polar(xy);
    float rad = polar.x + ampl*sin(freq * polar.x + phase);
    return polar2rect(vec2(rad, polar.y));
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    // Normalize mouse position:
    vec2 m = vec2(mouse.x/resolution.x, 1.0 - mouse.y/resolution.y);
    m.x = m.x * resolution.x / resolution.y;

    vec2 uv2 = sine_warp(uv - m, ampl, freq_r, -time*freq_t);
    float grey = grid(uv2, 0.1, 20.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-10').text());">Use code &raquo;</button></div>
</div>

<p>
Click around in the render, and also adjust the sliders.  Note that
adjusting <b>Ampl</b> to zero completely eliminates the effect.
</p>

<p>
<code>sine_warp</code> might take some explanation:
</p>

<ul class="org-ul">
<li>It converts the input coordinates (assumed to be rectangular
coordinates) to polar coordinates, \((r,\theta)\) - <code>polar.x</code> and
<code>polar.y</code> in the code.</li>
<li>It distorts radius \(r\) by adding a sinusoid to it.  This sine wave's
amplitude is <code>ampl</code>, it varies with radius by frequency <code>freq</code>, and
it has phase adjusted by <code>phase</code>.  Note that if <code>ampl=0</code>, \(r\) is
simply left as-is.</li>
<li>It converts the distorted radius and original angle back to
rectangular coordinates, and returns that.</li>
</ul>

<p>
Thus, we're distorting the entire grid based on its radius - i.e. its
distance from whatever the center of the coordinate space is, which in
this case is defined by the mouse click.  The nature of that
distortion is that peaks in the sinewave occur at certain distances,
and at those distances we've stretched space outward a bit (note how
the grid lines are further apart); troughs in the sinewave occur at
other distances, and at those distances we've forced space inward a
bit (note how the grid lines are closer together).  Since the pixels
that are a specific distance from some point form a circle, this gives
a sort of circular distortion which repeats as you move outward.
</p>

<p>
By typing <code>time</code> to this sinewave's phase (with <code>freq_t</code> to set how
fast that phase varies), we add some animation to it.
</p>

<p>
This is all trying to convey intuitively that when you have a function
that turns a point in space to something (such as color in this case),
warping that function's domain is distorting or transforming space in
some way.
</p>

<p>
We can transform the space multiple times too:
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-11' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;
uniform float time;
uniform vec4 mouse;

uniform float freq_r;
uniform float freq_t;
uniform float ampl;
uniform float freq_r2;
uniform float freq_t2;
uniform float ampl2;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = clamp(thickness - uv_mod.x, 0.0, 1.0);
    float y1 = clamp(thickness - uv_mod.y, 0.0, 1.0);
    return (min(x1, thickness-x1) + min(y1,thickness-y1))/thickness;
}

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(sqrt(xy.x * xy.x + xy.y * xy.y), atan(xy.y, xy.x));
    return polar;
}

vec2 polar2rect(vec2 polar)
{
    vec2 rect = vec2(polar.x * cos(polar.y), polar.x * sin(polar.y));
    return rect;
}

vec2 sine_warp(vec2 xy, float ampl, float freq, float phase)
{
    vec2 polar = rect2polar(xy);
    float rad = polar.x + ampl*sin(freq * polar.x + phase);
    return polar2rect(vec2(rad, polar.y));
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    // Normalize mouse position:
    vec2 m = vec2(mouse.x/resolution.x, 1.0 - mouse.y/resolution.y);
    m.x = m.x * resolution.x / resolution.y;

    vec2 uv2 = sine_warp(uv - m, ampl, freq_r, -time*freq_t);
    vec2 uv3 = sine_warp(uv2, ampl2, freq_r2, -time*freq_t2);
    float grey = grid(uv3, 0.1, 20.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-11' style='display:none'>/* PARAMETERS
[
   {
       "type": "float",
       "value": 27,
       "min": 0,
       "max": 50,
       "name": "freq_r",
       "GUIName": "FreqR"
   },
   {
       "type": "float",
       "value": 8,
       "min": 0,
       "max": 10,
       "name": "freq_t",
       "GUIName": "FreqT"
   },
   {
       "type": "float",
       "value": 0.015,
       "min": 0,
       "max": 0.2,
       "name": "ampl",
       "GUIName": "Ampl"
   },
   {
       "type": "float",
       "value": 5,
       "min": 0,
       "max": 50,
       "name": "freq_r2",
       "GUIName": "FreqR2"
   },
   {
       "type": "float",
       "value": 1.0,
       "min": 0,
       "max": 10,
       "name": "freq_t2",
       "GUIName": "FreqT2"
   },
   {
       "type": "float",
       "value": 0.08,
       "min": 0,
       "max": 0.2,
       "name": "ampl2",
       "GUIName": "Ampl2"
   }
]
END */
uniform vec2 resolution;
uniform float time;
uniform vec4 mouse;

uniform float freq_r;
uniform float freq_t;
uniform float ampl;
uniform float freq_r2;
uniform float freq_t2;
uniform float ampl2;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = clamp(thickness - uv_mod.x, 0.0, 1.0);
    float y1 = clamp(thickness - uv_mod.y, 0.0, 1.0);
    return (min(x1, thickness-x1) + min(y1,thickness-y1))/thickness;
}

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(sqrt(xy.x * xy.x + xy.y * xy.y), atan(xy.y, xy.x));
    return polar;
}

vec2 polar2rect(vec2 polar)
{
    vec2 rect = vec2(polar.x * cos(polar.y), polar.x * sin(polar.y));
    return rect;
}

vec2 sine_warp(vec2 xy, float ampl, float freq, float phase)
{
    vec2 polar = rect2polar(xy);
    float rad = polar.x + ampl*sin(freq * polar.x + phase);
    return polar2rect(vec2(rad, polar.y));
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    // Normalize mouse position:
    vec2 m = vec2(mouse.x/resolution.x, 1.0 - mouse.y/resolution.y);
    m.x = m.x * resolution.x / resolution.y;

    vec2 uv2 = sine_warp(uv - m, ampl, freq_r, -time*freq_t);
    vec2 uv3 = sine_warp(uv2, ampl2, freq_r2, -time*freq_t2);
    float grey = grid(uv3, 0.1, 20.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-11').text());">Use code &raquo;</button></div>
</div>

<p>
The below likewise transforms space twice, but in different ways this
time.  <code>angle_warp</code> distorts the angle in polar coordinates,
proportional to the radius; play with the <code>AngCoef</code> parameter to see
the effect.
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-12' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;
uniform float time;
uniform vec4 mouse;

uniform float freq_r;
uniform float freq_t;
uniform float ampl;
uniform float angle_coef;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = clamp(thickness - uv_mod.x, 0.0, 1.0);
    float y1 = clamp(thickness - uv_mod.y, 0.0, 1.0);
    return (min(x1, thickness-x1) + min(y1,thickness-y1))/thickness;
}

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(sqrt(xy.x * xy.x + xy.y * xy.y), atan(xy.y, xy.x));
    return polar;
}

vec2 polar2rect(vec2 polar)
{
    vec2 rect = vec2(polar.x * cos(polar.y), polar.x * sin(polar.y));
    return rect;
}

vec2 sine_warp(vec2 xy, float ampl, float freq, float phase)
{
    vec2 polar = rect2polar(xy);
    float rad = polar.x + ampl*sin(freq * polar.x + phase);
    return polar2rect(vec2(rad, polar.y));
}

vec2 angle_warp(vec2 xy, float a)
{
    vec2 polar = rect2polar(xy);
    float ang = polar.y + a*polar.x;
    return polar2rect(vec2(polar.x, ang));
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    // Normalize mouse position:
    vec2 m = vec2(mouse.x/resolution.x, 1.0 - mouse.y/resolution.y);
    m.x = m.x * resolution.x / resolution.y;

    vec2 uv2 = sine_warp(uv - m, ampl, freq_r, -time*freq_t);
    vec2 uv3 = angle_warp(uv2, angle_coef);
    float grey = grid(uv3, 0.1, 20.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-12' style='display:none'>/* PARAMETERS
[
   {
       "type": "float",
       "value": 17,
       "min": 0,
       "max": 50,
       "name": "freq_r",
       "GUIName": "FreqR"
   },
   {
       "type": "float",
       "value": 3.5,
       "min": 0,
       "max": 10,
       "name": "freq_t",
       "GUIName": "FreqT"
   },
   {
       "type": "float",
       "value": 0.02,
       "min": 0,
       "max": 0.2,
       "name": "ampl",
       "GUIName": "Ampl"
   },
   {
       "type": "float",
       "value": 0.1,
       "min": -2,
       "max": 2,
       "name": "angle_coef",
       "GUIName": "AngCoef"
   }
]
END */
uniform vec2 resolution;
uniform float time;
uniform vec4 mouse;

uniform float freq_r;
uniform float freq_t;
uniform float ampl;
uniform float angle_coef;

float grid(vec2 xy, float thickness, float count)
{
    vec2 uv_mod = mod(xy * count, 1.0);
    float x1 = clamp(thickness - uv_mod.x, 0.0, 1.0);
    float y1 = clamp(thickness - uv_mod.y, 0.0, 1.0);
    return (min(x1, thickness-x1) + min(y1,thickness-y1))/thickness;
}

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(sqrt(xy.x * xy.x + xy.y * xy.y), atan(xy.y, xy.x));
    return polar;
}

vec2 polar2rect(vec2 polar)
{
    vec2 rect = vec2(polar.x * cos(polar.y), polar.x * sin(polar.y));
    return rect;
}

vec2 sine_warp(vec2 xy, float ampl, float freq, float phase)
{
    vec2 polar = rect2polar(xy);
    float rad = polar.x + ampl*sin(freq * polar.x + phase);
    return polar2rect(vec2(rad, polar.y));
}

vec2 angle_warp(vec2 xy, float a)
{
    vec2 polar = rect2polar(xy);
    float ang = polar.y + a*polar.x;
    return polar2rect(vec2(polar.x, ang));
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    // Normalize mouse position:
    vec2 m = vec2(mouse.x/resolution.x, 1.0 - mouse.y/resolution.y);
    m.x = m.x * resolution.x / resolution.y;

    vec2 uv2 = sine_warp(uv - m, ampl, freq_r, -time*freq_t);
    vec2 uv3 = angle_warp(uv2, angle_coef);
    float grey = grid(uv3, 0.1, 20.0);
    
    gl_FragColor = vec4(grey, grey, grey, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-12').text());">Use code &raquo;</button></div>
</div>

<p>
We can swap out <code>grid</code> for any other function and see space distorted
in the same way; the grid was just used there to make it more obvious
how we are distorting space itself.  The below code does the same
transformations - but on the color gradient that was used early on in
this chapter.
</p>

<div class="org-src-container">
<juicy-ace-editor id='webgl-juicy-ace-editor-13' mode="ace/mode/glsl" readonly="true">uniform vec2 resolution;
uniform float time;
uniform vec4 mouse;

uniform float freq_r;
uniform float freq_t;
uniform float ampl;
uniform float angle_coef;

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(sqrt(xy.x * xy.x + xy.y * xy.y), atan(xy.y, xy.x));
    return polar;
}

vec2 polar2rect(vec2 polar)
{
    vec2 rect = vec2(polar.x * cos(polar.y), polar.x * sin(polar.y));
    return rect;
}

vec2 sine_warp(vec2 xy, float ampl, float freq, float phase)
{
    vec2 polar = rect2polar(xy);
    float rad = polar.x + ampl*sin(freq * polar.x + phase);
    return polar2rect(vec2(rad, polar.y));
}

vec2 angle_warp(vec2 xy, float a)
{
    vec2 polar = rect2polar(xy);
    float ang = polar.y + a*polar.x;
    return polar2rect(vec2(polar.x, ang));
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    // Normalize mouse position:
    vec2 m = vec2(mouse.x/resolution.x, 1.0 - mouse.y/resolution.y);
    m.x = m.x * resolution.x / resolution.y;

    vec2 uv2 = sine_warp(uv - m, ampl, freq_r, -time*freq_t);
    vec2 uv3 = angle_warp(uv2, angle_coef);
    
    gl_FragColor = vec4(uv3.x, uv3.y, 0.0, 1.0);
}
</juicy-ace-editor><div id='webgl-full-code-13' style='display:none'>/* PARAMETERS
[
   {
       "type": "float",
       "value": 17,
       "min": 0,
       "max": 50,
       "name": "freq_r",
       "GUIName": "FreqR"
   },
   {
       "type": "float",
       "value": 3.5,
       "min": 0,
       "max": 10,
       "name": "freq_t",
       "GUIName": "FreqT"
   },
   {
       "type": "float",
       "value": 0.02,
       "min": 0,
       "max": 0.2,
       "name": "ampl",
       "GUIName": "Ampl"
   },
   {
       "type": "float",
       "value": 0.1,
       "min": -2,
       "max": 2,
       "name": "angle_coef",
       "GUIName": "AngCoef"
   }
]
END */
uniform vec2 resolution;
uniform float time;
uniform vec4 mouse;

uniform float freq_r;
uniform float freq_t;
uniform float ampl;
uniform float angle_coef;

vec2 rect2polar(vec2 xy)
{
    vec2 polar = vec2(sqrt(xy.x * xy.x + xy.y * xy.y), atan(xy.y, xy.x));
    return polar;
}

vec2 polar2rect(vec2 polar)
{
    vec2 rect = vec2(polar.x * cos(polar.y), polar.x * sin(polar.y));
    return rect;
}

vec2 sine_warp(vec2 xy, float ampl, float freq, float phase)
{
    vec2 polar = rect2polar(xy);
    float rad = polar.x + ampl*sin(freq * polar.x + phase);
    return polar2rect(vec2(rad, polar.y));
}

vec2 angle_warp(vec2 xy, float a)
{
    vec2 polar = rect2polar(xy);
    float ang = polar.y + a*polar.x;
    return polar2rect(vec2(polar.x, ang));
}

void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    // Correct aspect:
    uv.x = uv.x * resolution.x / resolution.y;

    // Normalize mouse position:
    vec2 m = vec2(mouse.x/resolution.x, 1.0 - mouse.y/resolution.y);
    m.x = m.x * resolution.x / resolution.y;

    vec2 uv2 = sine_warp(uv - m, ampl, freq_r, -time*freq_t);
    vec2 uv3 = angle_warp(uv2, angle_coef);
    
    gl_FragColor = vec4(uv3.x, uv3.y, 0.0, 1.0);
}
</div><div class='no-print' align="left"><button type="button" onclick="copy_code($('#webgl-full-code-13').text());">Use code &raquo;</button></div>
</div>
</div>
</div>

<div id="outline-container-org87eb884" class="outline-3">
<h3 id="org87eb884"><span class="section-number-3"> 2.7</span> Summary</h3>
<div class="outline-text-3" id="text-1-7">
<p>
The intention of this section was to give a crash course in how
shaders work in this context, how functions are a meaningful
representation for graphics, and how transformations on the function's
domain can be visualized as warping space.
</p>

<p>
This whole section was focused on the use of shaders to create 2D
graphics.  The next section extends this to their use in creating a 3D
renderer.
</p>
</div>
</div>
</div>
</div>
</body>
</html>
